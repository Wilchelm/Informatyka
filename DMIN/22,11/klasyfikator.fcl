FUNCTION_BLOCK klasyfikator

VAR_INPUT
	wiek : REAL;
	doswiadczenie : REAL;
	inteligencja : REAL;
END_VAR

VAR_OUTPUT
	klasyfikator : REAL;
END_VAR

FUZZIFY wiek
	TERM mlody := (0,1) (20,1) (25,0);
	TERM sredni:= (20,0) (30,1) (40,1) (50,0);
	TERM stary := (40,0) (55,1) (200,1) ;
END_FUZZIFY


FUZZIFY doswiadczenie
	TERM male := (0,1) (10,0);
	TERM srednie := (5,0) (10,1) (15,1) (20,0);
	TERM duze := (15,0) (20,1) (25,1) (100, 1);
END_FUZZIFY


FUZZIFY inteligencja
	TERM mala := (0,1) (100,0);
	TERM srednia := (50,0) (100,1) (150,0);
	TERM wysoka := (100,0)(150,1) (300,1);
END_FUZZIFY

DEFUZZIFY klasyfikator		
	TERM slaby := (0,1) (50,0);
	TERM przecietny := (40,0) (50,1) (60,0);
	TERM dobry := (60,0) (70,1) (80,0);
	TERM bardzo_dobry := (70,0)(90,1);
	METHOD : COG;		// Metoda defuzyfikacji - 'Center Of Gravity' - 'Sreodka ciezkosci'
	DEFAULT := 0;		
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Uzycie 'min' jako 'and' (implikuje uzycie 'max' jako 'or' dla zapewnienia praw DeMorgan'a)
	ACT : MIN;			// Uzycie  'min' jako metody aktywacji
	ACCU : MAX;			// Metoda agregacji - 'max'

	RULE 1 : IF wiek IS mlody AND doswiadczenie IS duze THEN klasyfikator IS bardzo_dobry;
	RULE 2 : IF wiek IS mlody AND doswiadczenie IS duze AND inteligencja IS mala THEN klasyfikator IS dobry;
	RULE 3 : IF wiek IS mlody AND doswiadczenie IS srednie THEN klasyfikator IS dobry;
	RULE 4 : IF wiek IS mlody AND doswiadczenie IS male THEN klasyfikator IS przecietny;
	
	RULE 5 : IF wiek IS sredni AND doswiadczenie IS duze THEN klasyfikator IS bardzo_dobry;
	RULE 6 : IF wiek IS sredni AND doswiadczenie IS srednie AND inteligencja IS mala THEN klasyfikator IS przecietny;
	RULE 7 : IF wiek IS sredni AND doswiadczenie IS srednie THEN klasyfikator IS dobry;
	RULE 8 : IF wiek IS sredni AND doswiadczenie IS male THEN klasyfikator IS przecietny;
	
	RULE 9 : IF wiek IS stary AND doswiadczenie IS duze THEN klasyfikator IS dobry;
	RULE 10 : IF wiek IS stary AND inteligencja IS mala THEN klasyfikator IS slaby;
	RULE 11 : IF wiek IS stary AND doswiadczenie IS srednie THEN klasyfikator IS przecietny;
	RULE 12 : IF wiek IS stary AND doswiadczenie IS male THEN klasyfikator IS slaby;
END_RULEBLOCK

END_FUNCTION_BLOCK